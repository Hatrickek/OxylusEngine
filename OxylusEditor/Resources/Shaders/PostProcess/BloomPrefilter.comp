#version 460 core
#pragma shader_stage(compute)
#define EPSILON 0.0001

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0) restrict writeonly uniform image2D ImgResult;
layout(binding = 1) uniform sampler2D Color;

layout(push_constant, std140) uniform uPC {
  // x: threshold, y: clamp, z: radius, w: unused
  layout(offset = 0) vec4 Params;
};

vec3 Downsample(sampler2D src, vec2 uv, float lod);
vec3 Prefilter(vec3 color);

void main()
{
    ivec2 imgCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imgSize = imageSize(ImgResult);
    if (any(greaterThanEqual(imgCoord, imgSize)))
        return;

    vec2 uv = (imgCoord + 0.5) / imgSize;
    
    vec3 result = Downsample(Color, uv, 0);
    result = Prefilter(result);
    imageStore(ImgResult, imgCoord, vec4(result, 1.0));
}

vec3 Downsample(sampler2D src, vec2 uv, float lod)
{
    vec3 center = textureLod(src, uv, lod).rgb;
    vec3 yellowUpRight  = textureLodOffset(src, uv, lod, ivec2( 0,  2)).rgb;
    vec3 yellowDownLeft = textureLodOffset(src, uv, lod, ivec2(-2,  0)).rgb;
    vec3 greenDownRight = textureLodOffset(src, uv, lod, ivec2( 2,  0)).rgb;
    vec3 blueDownLeft   = textureLodOffset(src, uv, lod, ivec2( 0, -2)).rgb;

    vec3 yellow  = textureLodOffset(src, uv, lod, ivec2(-2,  2)).rgb;
    yellow      += yellowUpRight;
    yellow      += center;
    yellow      += yellowDownLeft;

    vec3 green = yellowUpRight;
    green     += textureLodOffset(src, uv, lod, ivec2( 2,  2)).rgb;
    green     += greenDownRight;
    green     += center;

    vec3 blue = center;
    blue     += greenDownRight;
    blue     += textureLodOffset(src, uv, lod, ivec2( 2, -2)).rgb;
    blue     += blueDownLeft;

    vec3 lila  = yellowDownLeft;
    lila      += center;
    lila      += blueDownLeft;
    lila      += textureLodOffset(src, uv, lod, ivec2(-2, -2)).rgb;

    vec3 red = textureLodOffset(src, uv, lod, ivec2(-1,  1)).rgb;
    red     += textureLodOffset(src, uv, lod, ivec2( 1,  1)).rgb;
    red     += textureLodOffset(src, uv, lod, ivec2( 1, -1)).rgb;
    red     += textureLodOffset(src, uv, lod, ivec2(-1, -1)).rgb;

    return (red * 0.5 + (yellow + green + blue + lila) * 0.125) * 0.25;
}

vec3 Prefilter(vec3 color)
{
    const float Knee = 0.2;
    color = min(vec3(Params.y), color);

    float brightness = max(max(color.r, color.g), color.b);

    vec3 curve = vec3(Params.x - Knee, Knee * 2.0, 0.25 / Knee);
    float rq = clamp(brightness - curve.x, 0.0, curve.y);
    rq = (rq * rq) * curve.z;
    color *= max(rq, brightness - Params.x) / max(brightness, EPSILON);
    
    return color;
}